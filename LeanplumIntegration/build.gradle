plugins {
    id 'com.android.library'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'maven-publish'
    id 'signing'
}

group = project.bintrayGroupId
version = project.bintrayPackageVersion

android {
    compileSdkVersion 31

    defaultConfig {
        minSdkVersion 15
        versionCode 1
        versionName project.bintrayPackageVersion
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    //noinspection GradleDynamicVersion
    compileOnly 'com.segment.analytics.android:analytics:[4.0,5.0['
    //noinspection GradleDynamicVersion
    implementation 'com.leanplum:leanplum-core:[5.0,7.0['
}

afterEvaluate {
    publishing {
        publications {
            aar(MavenPublication) {
                groupId project.bintrayGroupId
                version project.bintrayPackageVersion
                artifactId project.bintrayArtifactId

                // Apply the component for the release build variant
                from components.release

                pom {
                    packaging = 'aar'
                    name = project.bintrayArtifactId
                    description = project.bintrayDescription
                    url = 'https://github.com/' + project.bintrayGithub
                    licenses {
                        license {
                            name = 'The Apache Software License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                            distribution = 'repo'
                        }
                    }
                    developers {
                        developer {
                            name = project.bintrayAuthor
                        }
                    }
                    scm {
                        url = 'https://github.com/' + project.bintrayGithub
                    }
                }
            }
        }

        repositories {
            maven {
                name = "sonatype"
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"

                credentials {
                    username = project.sonatypeOssrhUser
                    password = project.sonatypeOsshrPassword
                }
            }
        }
    }
}

artifactory {
    contextUrl = 'https://repo.leanplum.com'
    publish {
        repository {
            repoKey = '/'

            username = project.artifactoryUser
            password = project.artifactoryPassword
        }
        defaults {
            publications('aar')
            publishArtifacts = true

            properties = ['qa.level': 'basic', 'q.os': 'android', 'dev.team': 'core']
            publishPom = true
        }
    }
}

signing {
    def signingKey = null
    def signingKeyBase64 = project.sonatypeSigningKeyBase64
    if (signingKeyBase64 != null) {
        signingKey = new String(Base64.getDecoder().decode((String) signingKeyBase64))
    }
    def signingPassword = project.sonatypeSigningKeyPassword

    useInMemoryPgpKeys(signingKey, signingPassword)
    sign publishing.publications
}
